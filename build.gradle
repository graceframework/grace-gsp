buildscript {
    repositories {
        // mavenLocal()
        mavenCentral()
        // maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
    }
}

ext."signing.keyId" = project.hasProperty("signing.keyId") ? project.getProperty('signing.keyId') : System.getenv('SIGNING_KEY')
ext."signing.password" = project.hasProperty("signing.password") ? project.getProperty('signing.password') : System.getenv('SIGNING_PASSPHRASE')
ext."signing.secretKeyRingFile" = project.hasProperty("signing.secretKeyRingFile") ? project.getProperty('signing.secretKeyRingFile') : ("${System.properties['user.home']}${File.separator}.gnupg${File.separator}secring.gpg")
ext.isReleaseVersion = !projectVersion.endsWith("SNAPSHOT")

version = project.projectVersion
group = "org.graceframework"

apply plugin: "java-library"
apply plugin: "io.github.gradle-nexus.publish-plugin"
apply plugin: "maven-publish"
apply plugin: "signing"
apply plugin: "idea"

allprojects {
    repositories {
        // mavenLocal()
        mavenCentral()
        // maven { url "https://repo.grails.org/grails/core" }
        if (!isReleaseVersion) {
            maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        }
    }
}

subprojects { project ->
    version = project.projectVersion

    ext {
        userOrg = "graceframework"
        isPlugin = project.name.startsWith('grace-plugin')
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    if (isPlugin) {
        group "org.graceframework.plugins"
    } else {
        group "org.graceframework"
    }

    configurations {
        documentation
    }

    apply plugin: "groovy"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    configurations {
        documentation
    }

    dependencies {
        api "org.codehaus.groovy:groovy:$groovyVersion"
        compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"

        testImplementation "javax.servlet:javax.servlet-api:$servletApiVersion"
        testImplementation "org.codehaus.groovy:groovy-test-junit5:${groovyVersion}"
        testImplementation("org.spockframework:spock-core:${spockVersion}") {
                exclude group:'junit', module: 'junit-dep'
                exclude group:'org.codehaus.groovy', module: 'groovy-all'
        }
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
        testImplementation "org.junit.platform:junit-platform-runner:1.8.2"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"

        documentation "org.fusesource.jansi:jansi:2.4.0"
        documentation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"
        documentation 'info.picocli:picocli:4.6.3'
        documentation ("com.github.javaparser:javaparser-core:$javaParserCoreVersion")
    }

    compileTestGroovy {
        configure(groovyOptions.forkOptions) {
            jvmArgs = [ '-Xmx768m']
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        failOnError = false
    }

    configure([javadoc]) {
        options.encoding "UTF-8"
        options.docEncoding "UTF-8"
        options.charSet "UTF-8"
        options.jFlags "-Xms64M", "-Xmx512M"
    }

    configure([groovydoc]) {
        classpath += configurations.documentation
    }

    publishing {
        publications {
            maven(MavenPublication) {
                if (project.name.startsWith('grace-plugin')) {
                    artifactId(project.name - 'grace-plugin-')
                }
                else {
                    artifactId project.name
                }
                groupId = project.group
                version = project.version

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                from components.java

                pom {
                    name = "GSP"
                    description = "Groovy Server Page"
                    url = 'https://github.com/graceframework/grace-gsp'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'rainboyan'
                            name = 'Michael Yan'
                            email = 'rain@rainboyan.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/graceframework/grace-gsp.git'
                        developerConnection = 'scm:git:ssh://github.com:graceframework/grace-gsp.git'
                        url = 'https://github.com/graceframework/grace-gsp/tree/2022.0.x'
                    }
                }
            }
        }
    }

    afterEvaluate {
        signing {
            required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
            sign publishing.publications.maven
        }
    }
}

project.afterEvaluate {
    allprojects.repositories.each { handler ->
        handler.each {
            if (it.url.toString().startsWith("http://")) {
                throw new RuntimeException("Build should not define insecure HTTP-based Maven repostories")
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            def ossUser = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
            def ossPass = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
            def ossStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: project.hasProperty("sonatypeOssStagingProfileId") ? project.sonatypeOssStagingProfileId : ''
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = ossUser
            password = ossPass
            stagingProfileId = ossStagingProfileId
        }
    }
}